
AC_CONFIG_SRCDIR([graphs/graph_bundle.py])
AC_CONFIG_MACRO_DIR([m4])
AC_INIT([python-sage], [none], [sage-support@googlegroups.com])
AM_SILENT_RULES
AC_CONFIG_AUX_DIR([.])
AC_CONFIG_HEADER([config.h])

AM_INIT_AUTOMAKE([-Wall -Werror foreign -Wno-portability])

# Use the silent-rules feature when possible.
m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

# this is not in stable automake yet
# AM_EXTRA_RECURSIVE_TARGETS([py])

AM_PROG_AR
LT_INIT([disable-static])

AC_PROG_CC
AC_PROG_CXX
AC_PROG_LN_S

## environment,
# see autoconf/manual/autoconf-2.60/html_node/Setting-Output-Variables.html,
# particularly: "variable is kept during automatic reconfiguration"
# FIXME: LD_LIBRARY_PATH required??
AC_ARG_VAR(CPPFLAGS, cppflags)
AC_ARG_VAR(LDFLAGS, ldflags)
AC_ARG_VAR(PATH, path)
AC_ARG_VAR(PYTHONPATH, pythonpath)

AC_ARG_ENABLE([doc], AS_HELP_STRING([--enable-doc], [Build docs]),
 [case "${enableval}" in
  yes) doc=true ;;
  no)  doc=false ;;
  *) AC_MSG_ERROR([bad value ${enableval} for --enable-doc]) ;;
esac],[doc=no])
AM_CONDITIONAL([BUILD_DOC], [test x$doc = xtrue])

sage_local=$prefix
test "x$prefix" = xNONE && sage_local=$ac_default_prefix
AC_SUBST(SAGE_LOCAL, $sage_local)

## VPATH
AM_CONDITIONAL([VPATH_BUILD], [test ! x$srcdir = "x."])

# FIXME: use VPATH_BUILD_{TRUE/FALSE}
VPATH_TRUE=
VPATH_FALSE=#
if test x$srcdir = "x."; then
  VPATH_TRUE=#
  VPATH_FALSE=
fi
AC_SUBST(VPATH_TRUE)
AC_SUBST(VPATH_FALSE)
## defaults
AC_SUBST(AM_LDFLAGS, "-module -avoid-version")

# doesn't work... (how to set a default?!)
#AC_SUBST([AM_DEFAULT_SOURCE_EXT], [.pyx])

## python stuff
python_version_required=2.7
AM_PATH_PYTHON([$python_version_required])
if ! test x$PYTHON_VERSION = x"$python_version_required"; then
   AC_MSG_ERROR([python $PYTHON_VERSION is not $python_version_required])
fi
AM_PATH_PYTHON([2.7])

AC_PATH_PROGS([CYTHON],
              [cython2.7 cython],
              [no])
AS_IF([test "$CYTHON" = no], [AC_MSG_ERROR([cannot find cython.])])

AM_CHECK_PYTHON_HEADERS()
PYTHON_INCLUDE_DIR=$PYTHON_PREFIX/include
PYTHON_LIB_DIR=$PYTHON_PREFIX/lib

AC_ARG_WITH(python-include-dir,
            [  --with-python-include-dir=DIR look in DIR for Python headers],
            [PYTHON_INCLUDE_DIR=$withval],)
AC_SUBST(PYTHON_INCLUDE_DIR)
my_CPPFLAGS="$CPPFLAGS $PYTHON_INCLUDES"
AC_ARG_WITH(python-lib-dir,
            [  --with-python-lib-dir=DIR look in DIR for Python libraries],
            [PYTHON_LIB_DIR=$withval],)
AC_SUBST(PYTHON_LIB_DIR)

AC_MSG_CHECKING([whether jinja2 is available])
if ! $PYTHON -c import\ jinja2 &>/dev/null; then
   AC_MSG_RESULT([no])
   AC_MSG_ERROR([no jinja2])
fi
AC_MSG_RESULT([yes])

AC_MSG_CHECKING([whether $CYTHON supports dependencies])
if $CYTHON --help | grep -q 'dependency makefile'; then
   pydep=yes
else
   pydep=no
fi
AC_MSG_RESULT($pydep)
AM_CONDITIONAL([PYDEP], [test $pydep = yes])

AC_MSG_CHECKING([whether $CYTHON supports gdb])
if $CYTHON --help | grep -q 'gdb-outdir'; then
   cygdb=yes
else
   cygdb=no
fi
AC_MSG_RESULT($cygdb)
AM_CONDITIONAL([CYGDB], [test $cygdb = yes])

# find clib. might be obsolete
# AC_ARG_WITH(blas, --with-clib   choose c_lib, [clib=$with_clib], [])

AC_CHECK_HEADERS([math.h assert.h sys/sysmacros.h fcntl.h sys/stat.h mach/mach.h sys/mman.h])
AC_CHECK_HEADERS([sgtty.h stdlib.h string.h sys/ioctl.h sys/param.h sys/time.h sys/resource.h])
AC_CHECK_HEADERS([termio.h termios.h unistd.h signal.h stdio.h libc.h sys/times.h sys/wait.h])
AC_CHECK_HEADERS([vfork.h dirent.h errno.h setjmp.h])

AC_FUNC_VFORK
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_CHECK_FUNCS([getpseudotty _getpty getpt ptsname_r])
AC_CHECK_FUNCS([setpgid setjmp sbrk])
AC_CHECK_FUNCS([rld_load])
AC_CHECK_FUNCS([times getrusage])
AC_CHECK_FUNCS([vm_allocate])
AC_CHECK_FUNCS([fork popen waitpid wait4])
AC_CHECK_FUNCS([dup2 floor memmove mkdir pow rint])
AC_CHECK_FUNCS([ttyname strerror select])
AC_CHECK_FUNCS([access stat fstat lstat unlink mkdir mkdtemp mkstemp rmdir link rename])
AC_CHECK_FUNCS([symlink readlink chmod fchmod chown fchown lchown dup dup2 mknod mkfifo])
AC_CHECK_FUNCS([socket listen accept connect bind getsockname recv recvfrom recvmsg send])
AC_CHECK_FUNCS([sendto sendmsg getsockopt setsockopt getprotobyname gethostbyname])
AC_CHECK_FUNCS([opendir closedir dirfd readdir rewinddir seekdir telldir])

### singular
AC_PATH_PROGS([SINGULAR],
              [Singular singular],
              [no])
AS_IF([test "$SINGULAR" = no], [AC_MSG_ERROR([cannot find singular.])])
AS_IF([$SINGULAR -q],,[AC_MSG_ERROR([singular doesn't work.])])
SINGULAR_LIB=`echo 'system("SingularLib");' | $SINGULAR -q --no-rc --no-stdlib -b 2>/dev/null`
if test ! -e $SINGULAR_LIB/algebra.lib; then
    AC_MSG_ERROR([unexpected singular install, cannot find lib])
fi
AC_SUBST([SINGULAR_LIB])
SINGULAR_BIN=`dirname $SINGULAR`
AC_SUBST([SINGULAR_BIN])
SINGULAR_SO=`readlink -f \`$CC -print-file-name=libsingular.so\``
AC_SUBST([SINGULAR_SO])

# singular needs singular and factory
OLDCPPFLAGS=$CPPFLAGS
SINGULAR_INCLUDES="-I$SINGULAR_BIN/../include/singular -I$SINGULAR_BIN/../include/factory"
CPPFLAGS="$SINGULAR_INCLUDES $CPPFLAGS"
AC_CHECK_HEADER([si_gmp.h],,[AC_MSG_ERROR(cannot find singular header)])
AC_SUBST([SINGULAR_INCLUDES])
CPPFLAGS=$OLDCPPFLAGS

AS_IF([test -f $sage_local/include/flint/flint.h],
      [LIBFLINT_INCLUDES=-I$sage_local/include/flint],
      [LIBFLINT_INCLUDES=-I/usr/include/flint])
CPPFLAGS+=" $LIBFLINT_INCLUDES"
AC_CHECK_HEADER([flint.h],,[AC_MSG_ERROR(cannot find flint header)])
AC_SUBST(LIBFLINT_INCLUDES)

CPPFLAGS="$OLDCPPFLAGS $PYTHON_INCLUDES"
dnl FIXME: this check fails for some reason, should be AC_MSG_ERROR
AC_CHECK_HEADER([pynac/order.h],,[AC_MSG_NOTICE(cannot find pynac header)])

CPPFLAGS=$OLDCPPFLAGS

# just check, for now
AC_CHECK_HEADER([cliquer/cliquer.h],,[AC_MSG_ERROR(cannot find cliquer header)])
AC_CHECK_HEADER([zn_poly/zn_poly.h],,[AC_MSG_ERROR(cannot find zn_poly header)])

AC_LANG_PUSH([C++])
AC_CHECK_HEADER([fplll.h],,[AC_MSG_ERROR(cannot find fplll header)])
AC_CHECK_HEADER([fplll/fplll.h],,[AC_MSG_ERROR(cannot find fplll header)])
AC_LANG_POP
AC_SUBST([FPLLL_INCLUDES]) dnl currently unneeded

# extra packages: FIXME: just allow sage packages, if desired
AC_CHECK_HEADER([coxeter3.h], [HAVE_LIBCOXETER3=yes], [HAVE_LIBCOXETER3=no])
AC_SUBST([HAVE_LIBCOXETER3])
AM_CONDITIONAL([USE_LIBCOXETER3], [ test $HAVE_LIBCOXETER3 = yes ])

AC_CHECK_HEADER([fes.h], [HAVE_LIBFES=yes], [HAVE_LIBFES=no])
AC_SUBST([HAVE_LIBFES])
AM_CONDITIONAL([USE_LIBFES], [ test $HAVE_LIBFES = yes ])

# lrcalc
old_CPPFLAGS=$CPPFLAGS
HAVE_LIBLRCALC=no
AC_ARG_WITH(lrcalc,
    [  --with-lrcalc=lrcalc prefix],
    [   AC_MSG_RESULT([$with_lrcalc])
        case $with_lrcalc in
          no)
            ;;
          yes)
            dnl try to find it somewhere here... (better idea?!)
            LIBLRCALC_INCLUDES="-I$sage_local/include/lrcalc"
            HAVE_LIBLRCALC=yes
            CPPFLAGS+=" $LIBLRCALC_INCLUDES"
            AC_CHECK_HEADER([symfcn.h],,AC_MSG_ERROR([cannot find lrcalc]))
            ;;
          *)
            dnl use specified lrcalc
            LIBLRCALC_INCLUDES="-I$with_lrcalc/include/lrcalc"
            CPPFLAGS+=" $LIBLRCALC_INCLUDES"
            AC_CHECK_HEADER([symfcn.h],
                            [HAVE_LIBLRCALC=yes],
                            [LIBLRCALC_INCLUDES=
                            ])
            ;;
        esac
        ],[ dnl figure out automatically (the sage-the-distribution case)
            LIBLRCALC_INCLUDES="-I$sage_local/include/lrcalc"
            CPPFLAGS+=" $LIBLRCALC_INCLUDES"
            AC_CHECK_HEADER([symfcn.h],
                            [HAVE_LIBLRCALC=yes],
                            [LIBLRCALC_INCLUDES=
                            ])
    ]
)
CPPFLAGS=$old_CPPFLAGS
AC_SUBST([LIBLRCALC_INCLUDES])
AC_SUBST([HAVE_LIBLRCALC])
dnl AC_CHECK_LIB([lrcalc], [lrcalc], lrcalc=1))
AM_CONDITIONAL([USE_LIBLRCALC], [ test $HAVE_LIBLRCALC = yes ])

# FIXME: also check for gap
HAVE_LIBGAP=no
AC_CHECK_HEADER([libgap/libgap.h], [HAVE_LIBGAP=yes], [AC_MSG_NOTICE(cannot find libgap header);
   SAGE_CHECK_PYTHON_MODULE([ply.lex],[AC_MSG_ERROR([need ply to hack gap])]) ])
AM_CONDITIONAL([HAVE_LIBGAP], [ test $HAVE_LIBGAP = yes ])

AC_CHECK_HEADER([gurobi.h], [HAVE_LIBGUROBI=yes], [HAVE_LIBGUROBI=no])
AC_SUBST([HAVE_LIBGUROBI])
AM_CONDITIONAL([USE_LIBGUROBI], [ test $HAVE_LIBGUROBI = yes ])

AC_CHECK_HEADER([cbc.h], [HAVE_LIBCBC=yes], [HAVE_LIBCBC=no])
AC_SUBST([HAVE_LIBCBC])
AM_CONDITIONAL([USE_LIBCBC], [ test $HAVE_LIBCBC = yes ])

AC_CHECK_HEADER([cplex.h], [HAVE_LIBCPLEX=yes], [HAVE_LIBCPLEX=no])
AC_SUBST([HAVE_LIBCPLEX])
AM_CONDITIONAL([USE_LIBCPLEX], [ test $HAVE_LIBCPLEX = yes ])

AC_CHECK_HEADER([coin.h], [HAVE_LIBCOIN=yes], [HAVE_LIBCOIN=no])
AC_SUBST([HAVE_LIBCOIN])
AM_CONDITIONAL([USE_LIBCOIN], [ test $HAVE_LIBCOIN = yes ])

AC_CHECK_HEADER([Solver.h], [HAVE_LIBCRYPTOMINISAT=yes], [HAVE_LIBCRYPTOMINISAT=no])
AC_SUBST([HAVE_LIBCRYPTOMINISAT])
AM_CONDITIONAL([USE_LIBCRYPTOMINISAT], [ test $HAVE_LIBCRYPTOMINISAT = yes ])

AC_CHECK_LIB([readline], [main])
AC_SUBST(HAVE_LIBREADLINE)

# not fetching from environment. use --with-blas
blas=
blas2=

## Choose cblas library -- note -- make sure to update sage/misc/cython.py
## if you change this!!
AC_ARG_WITH(blas,   --with-blas   choose cblas, [blas=$with_blas], [])

if test -n "${blas}"; then
  AC_CHECK_LIB([$blas], [cblas_dgemv], [blas2=$blas],
               [ AC_MSG_ERROR(user specified blas doesnt exist) ]
              )
fi

if test -z "${blas}"; then
  # we cannot know whether this is sage-distributed atlas.
  # and it doesnt matter.
  AC_CHECK_LIB([atlas], [ATL_scpsc], [blas=cblas; blas2=atlas], [])
  # some random symbol   ^
fi

if test -z "${blas}"; then
  AC_CHECK_LIB([cblas], [cblas_dgemv], [blas=cblas; blas2=atlas], [])
fi

if test -z "${blas}"; then
  AC_CHECK_LIB([blas], [cblas_dgemv], [blas=gslcblas; blas2=gslcblas], [])
fi

if test -z "${blas}"; then
  AC_CHECK_LIB([gslcblas], [cblas_dgemv], [blas=gslcblas; blas2=gslcblas], [])
fi

if test -z "${blas}"; then
  AC_MSG_ERROR([couldn't find any blas])
fi

#if -z os.environ.has_key('SAGE_BLAS'):
#    BLAS=os.environ['SAGE_BLAS']
#    BLAS2=os.environ['SAGE_BLAS']
#   AC_MSG_RESULT(from environment: ${BLAS})
#elif os.path.exists('%s/lib/libatlas.so'%os.environ['SAGE_LOCAL']):
#    BLAS='cblas'
#    BLAS2='atlas'
#elif os.path.exists('/usr/lib/libcblas.dylib') or \
#     os.path.exists('/usr/lib/libcblas.so'):
#    BLAS='cblas'
#    BLAS2='atlas'
#elif os.path.exists('/usr/lib/libblas.dll.a'):
#    BLAS='gslcblas'
#    BLAS2='gslcblas'
#else:
#    # This is very slow  (?), but *guaranteed* to be available.
#    BLAS='gslcblas'
#    BLAS2='gslcblas'
#   AC_MSG_RESULT(fallback to gslcblas)

# random symbol (fixme?)
AC_CHECK_LIB([ec], [_Z9show_timeRSo], [],
             [ AC_MSG_ERROR([Could not find ec library]) ])

#########################################################
### M4RI flags
#########################################################

PKG_CHECK_MODULES([M4RI],[m4ri],,AC_MSG_ERROR([no m4ri config found]))
M4RI_CFLAGS+=" -std=c99"
AC_SUBST([M4RI_CFLAGS])

PKG_CHECK_MODULES([M4RIE],[m4rie],,
                  [AC_MSG_NOTICE([no m4rie config found, lets try without...])]
                  [M4RIE_CFLAGS=-I/usr/include/m4rie]
                 )

old_CPPFLAGS=$CPPFLAGS
dnl hack: pass -c99 via CPPFLAGS...
CPPFLAGS+=" $M4RIE_CFLAGS -std=c99"
AC_CHECK_HEADER([mzed.h],,[AC_MSG_ERROR(cannot find m4rie header)])
CPPFLAGS=$old_CPPFLAGS

AC_SUBST(BLAS, ${blas})
AC_SUBST(BLAS2, ${blas2})

# ouch?
my_CPPFLAGS+=' -I$(top_srcdir)/..'
AC_SUBST([my_CPPFLAGS])

# more paths
c_lib_include='$(top_srcdir)/../c_lib/include'
CSAGE_INCLUDES=-I$c_lib_include
LIBCSAGE_INCLUDES=-I$c_lib_include
AC_SUBST(CSAGE_INCLUDES)
AC_SUBST(LIBCSAGE_INCLUDES)

# fixme: use AC_...
numpy_include_dir=$sage_local/lib/python/site-packages/numpy/core/include
NUMPY_INCLUDES=-I$numpy_include_dir
AC_SUBST(numpy_include_dir)
AC_SUBST(NUMPY_INCLUDES)

SAGE_INC=$sage_local/include
AC_SUBST(SAGE_INC)

SAGE_DIR=$srcdir
AC_SUBST(SAGE_DIR)

AS_AC_EXPAND(SAGE_ETC, $sysconfdir)

AC_CHECK_SIZEOF(void *)
# AC_CHECK_ALIGNOF([long])
AC_HEADER_STDBOOL
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_DIRENT
AC_HEADER_MAJOR
AC_HEADER_STAT
AC_C_INLINE
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([signal])
AC_TYPE_SIGNAL
# weird check for signal handling
if test "$ac_cv_type_signal" = "void"; then
    AC_DEFINE([HAVE_SIGNAL_VOID], [1] ,[void as signal type??])
fi

AC_DEFINE(HAVE_STRERROR, 1, [Everybody has that])
AC_DEFINE(HAVE_SYS_ERRLIST, 1, [Or GAP will define its own])

#### data packages ####
# default: sort of pkgdatadir
AC_ARG_WITH(dbdir,
            AS_HELP_STRING([--with-dbdir=DIR].[use databases from DIR]),
            [dbdir=$withval],
            [dbdir=$sage_local/share])

AS_IF([test -d $dbdir/conway_polynomials],
      [CONWAY_DATA_DIR=$dbdir/conway_polynomials],
      [test -d /usr/share/sage/conway_polynomials], #gentoo
      [CONWAY_DATA_DIR=/usr/share/sage/conway_polynomials],
      [test -d /usr/share/sagemath/conway_polynomials], #debian
      [CONWAY_DATA_DIR=/usr/share/sagemath/conway_polynomials],
      [CONWAY_DATA_DIR=$sage_local/share/conway_polynomials]) dnl hack!
AC_SUBST([CONWAY_DATA_DIR])
AM_SUBST_NOTMAKE([CONWAY_DATA_DIR])

AS_IF([test -d $dbdir/graphs],
      [GRAPHS_DATA_DIR=$sage_local/share/graphs],
      [test -d /usr/share/sage/graphs],
      [GRAPHS_DATA_DIR=/usr/share/sage/graphs],
      [test -d /usr/share/sagemath/graphs],
      [GRAPHS_DATA_DIR=/usr/share/sagemath/graphs],
      [AC_MSG_ERROR([cannot find graphs database])])
AC_SUBST([GRAPHS_DATA_DIR])
AM_SUBST_NOTMAKE([GRAPHS_DATA_DIR])

AS_IF([test -d $dbdir/reflexive_polytopes],
      [POLYTOPE_DATA_DIR=$sage_local/share/reflexive_polytopes],
      [test -d /usr/share/sage/reflexive_polytopes],
      [POLYTOPE_DATA_DIR=/usr/share/sage/reflexive_polytopes],
      [test -d /usr/share/sagemath/reflexive_polytopes],
      [POLYTOPE_DATA_DIR=/usr/share/sagemath/reflexive_polytopes],
      [AC_MSG_ERROR([cannot find polytopes database])])
AC_SUBST([POLYTOPE_DATA_DIR])
AM_SUBST_NOTMAKE([POLYTOPE_DATA_DIR])

AS_IF([test -d $dbdir/ellcurves],
      [ELLCURVE_DATA_DIR=$dbdir/ellcurves],
      [test -d /usr/share/sage/ellcurves],
      [ELLCURVE_DATA_DIR=/usr/share/sage/ellcurves],
      [test -d /usr/share/sagemath/ellcurves],
      [ELLCURVE_DATA_DIR=/usr/share/sagemath/ellcurves],
      [AC_MSG_ERROR([cannot find elliptic curves])])
AC_SUBST([ELLCURVE_DATA_DIR])
AM_SUBST_NOTMAKE([ELLCURVE_DATA_DIR])

# FIXME: gentoo?
# FIXME: require GAP_INCLUDEDIR unless HAVE_LIBGAP
GAP_INCLUDEDIR=/usr/include/gap
AS_IF([test -d $sage_local/gap],
      [GAP_DATA_DIR=$sage_local/gap/latest;
       GAP_INCLUDEDIR=$sage_local/gap/latest/src],
      [test -d /usr/share/gap],
      [GAP_DATA_DIR=/usr/share/gap],
      [AC_MSG_ERROR([cannot find gap])])
AC_SUBST([GAP_DATA_DIR])
#AC_SUBST([GAP_INCLUDES])
AC_SUBST([GAP_INCLUDEDIR])

AC_MSG_CHECKING([for gap libdir])
AS_IF([test -x $sage_local/gap/latest/bin/*/gap],
      [GAP_LIB_DIR=`dirname $sage_local/gap/latest/bin/*/gap`],
      [test -d /usr/lib/gap/bin],
      [GAP_LIB_DIR=/usr/lib/gap/bin],
      [AC_MSG_ERROR([cannot find gap])])
AC_SUBST([GAP_LIB_DIR])
AC_MSG_RESULT([$GAP_LIB_DIR])

#### output files

AC_CONFIG_FILES([
    setup.py
    module_list.py
    Makefile
    algebras/letterplace/Makefile
    algebras/Makefile
    algebras/quatalg/Makefile
    calculus/Makefile
    categories/examples/Makefile
    categories/Makefile
    coding/Makefile
    combinat/Makefile
    combinat/matrices/Makefile
    combinat/words/Makefile
    crypto/Makefile
    databases/Makefile
    doctest/Makefile
    ext/interpreters/Makefile
    ext/Makefile
    finance/Makefile
    functions/Makefile
    games/Makefile
    geometry/Makefile
    geometry/triangulation/Makefile
    graphs/base/Makefile
    graphs/graph_decompositions/Makefile
    graphs/Makefile
    graphs/modular_decomposition/Makefile
    groups/abelian_gps/Makefile
    groups/Makefile
    groups/matrix_gps/Makefile
    groups/perm_gps/Makefile
    groups/perm_gps/partn_ref/Makefile
    gsl/Makefile
    homology/Makefile
    interacts/Makefile
    interfaces/Makefile
    lfunctions/Makefile
    libs/coxeter3/Makefile
    libs/cremona/Makefile
    libs/flint/Makefile
    libs/fplll/Makefile
    libs/gap/Makefile
    libs/lcalc/Makefile
    libs/lrcalc/Makefile
    libs/linbox/Makefile
    libs/Makefile
    libs/mpmath/Makefile
    libs/mwrank/Makefile
    libs/ntl/Makefile
    libs/pari/Makefile
    libs/singular/Makefile
    libs/symmetrica/Makefile
    logic/Makefile
    matrix/Makefile
    media/Makefile
    misc/Makefile
    modular/Makefile
    modular/arithgroup/Makefile
    modular/modform/Makefile
    modular/modsym/Makefile
    modules/fg_pid/Makefile
    modules/Makefile
    monoids/Makefile
    numerical/backends/Makefile
    numerical/Makefile
    parallel/Makefile
    plot/Makefile
    plot/plot3d/Makefile
    probability/Makefile
    quadratic_forms/Makefile
    rings/finite_rings/Makefile
    rings/function_field/Makefile
    rings/Makefile
    rings/number_field/Makefile
    rings/padics/Makefile
    rings/polynomial/Makefile
    rings/polynomial/padics/Makefile
    rings/semirings/Makefile
    rings/universal_cyclotomic_field/Makefile
    sandpiles/Makefile
    sat/Makefile
    sat/solvers/cryptominisat/Makefile
    sat/solvers/Makefile
    schemes/elliptic_curves/Makefile
    schemes/hyperelliptic_curves/Makefile
    schemes/Makefile
    schemes/toric/Makefile
    server/Makefile
    server/notebook/Makefile
    sets/Makefile
    stats/hmm/Makefile
    stats/Makefile
    structure/Makefile
    structure/proof/Makefile
    symbolic/integration/Makefile
    symbolic/Makefile
    tensor/Makefile
    tests/Makefile
    env.py
])

dnl create dummy files for cython file dependencies
dnl this is currently not natively supported by automake
AC_CONFIG_COMMANDS([cythondep],
                   [$ac_top_srcdir/cythondep AMDEP_TRUE=$AMDEP_TRUE])

AC_CONFIG_FILES([cythoninit],[chmod +x cythoninit])
AC_CONFIG_COMMANDS([cython_init],
                   [./cythoninit])

AC_SUBST(PYTHONPATH)

AC_OUTPUT

AC_MSG_RESULT([
** Configuration summary for $PACKAGE $VERSION:

   prefix:     $prefix
   exec_prefix:$exec_prefix
   libdir:     $libdir
   CPPFLAGS:   $CPPFLAGS
   CFLAGS:     $CFLAGS
   CXXFLAGS:   $CXXFLAGS
   LDFLAGS:    $LDFLAGS
   LIBS:       $LIBS
   c_lib:      $clib
   BLAS:       $blas
   BLAS2:      $blas2
   PYTHON:     $PYTHON

   flags:
   M4RI_CFLAGS:  $M4RI_CFLAGS
   M4RIE_CFLAGS: $M4RIE_CFLAGS

   directories/stuff
   gap data      $GAP_DATA_DIR
   gap lib       $GAP_LIB_DIR
   singular lib  $SINGULAR_LIB
   singular so   $SINGULAR_SO

   optional packages
   lrcalc:       $HAVE_LIBLRCALC
   cbc:          $HAVE_LIBCBC
   coxeter:      $HAVE_LIBCOXETER3
   gurobi:       $HAVE_LIBGUROBI
   cryptominisat $HAVE_LIBCRYPTOMINISAT
   libgap        $HAVE_LIBGAP
])

# vim:et:sw=2:
